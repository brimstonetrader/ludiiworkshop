(game "Monty Hall Problem"
    (players 1)
    (equipment {
        (board (rectangle 1 3))
        (piece "Square1" Shared)
        (piece "Square2" Shared)
        (piece "Square3" Shared)
    })
    
    (rules
        (start {
            (place Random {"Square1"})
            (place Random {"Square2"})
            (place Random {"Square3"})
            (set Hidden What (sites Board) to:P1)
        })
        
        phases:{
        (phase "FirstChoice"
            (play 
                (move Select (from (sites Board))
                    (then (set Hidden What 
                            (sites Random
                              (forEach (sites Board)  if:(and (!= (site) (last To)) 
								                              (!= (id "Square3" Shared) (what at:(site)))
								  				         )
                              ) num:1
                            ) False to:P1
                          )
                    )
                )
            ) (nextPhase "FinalChoice")
        )
        
        (phase "FinalChoice"
            (play 
                (move Select (from (sites Board)) 
                             (then (set Hidden What (sites Board) False to:P1))
                )
            )
            (end {
                (if ("IsPieceAt" "Square3" Shared (last To))      (result P1 Win))
                (if 
                    (or ("IsPieceAt" "Square1" Shared (last To)) 
					    ("IsPieceAt" "Square2" Shared (last To))) (result P1 Loss) 
                )
            }    )
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata
(info
{
(description "The Monty Hall problem is a brain teaser, in the form of a probability puzzle, loosely based on the American television game show Let's Make a Deal and named after its original host, Monty Hall.")
(rules "Monty Hall asks to choose one of three doors. One of the doors hides a car and the other two doors have a goat. You select which door you pick, but you donâ€™t open it right away.

The game opens one of the other two doors, and there is no prize behind it.

At this moment, there are two closed doors. You can keep the same choice or selecting the other door. If you get the car you win if not you loss.")
(id "1155")
(source "<a href=\"https://en.wikipedia.org/wiki/Monty_Hall_problem\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(version "1.3.10")
(classification "puzzle/planning")
(author "Steve Selvin")
(credit "Eric Piette")
(date "1975")
}
)
    
    (graphics {
    
        (piece Foreground "Square1"  image:"Goat"  fillColour:(colour Grey) edgeColour:(colour DarkGrey)    scale:0.7)
        (piece Foreground "Square2"  image:"Goat"  fillColour:(colour Grey) edgeColour:(colour DarkGrey) scale:0.7)
        (piece Foreground "Square3"  image:"Car"  fillColour:(colour White) edgeColour:(colour Red)   scale:0.7)
        (no Board)
        (player Colour Shared (colour Brown))
        (hiddenImage "door")
    })
)

