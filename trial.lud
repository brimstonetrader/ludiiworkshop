(game "Hex" 
    (players 2) 
    (equipment { 
        (board (hex Diamond 11)) 
        (piece "Marker" Each)
        (regions P1 {(sites Side NE) (sites Side SW) })
        (regions P2 {(sites Side NW) (sites Side SE) })
    }) 
    (rules 
        (play (move Add (to (sites Empty))))
        (end (if (is Connected Mover) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------


(metadata 
    
    (info
        {
        (description "Hex was invented by Piet Hein in 1942 then described again by John Nash in 1944. It is played on a rhombus tiled by hexagons, most commonly 11x11 in size.")
        (rules "Players take turns placing a piece of their colour at an empty cell, and win by connecting their board sides with a chain of their pieces.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Hex_(board_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "82")
        (version "1.3.11")
        (classification "board/space/connection")
        (credit "Eric Piette")
        (date "1942")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (board Colour Phase0 (colour VeryLightGrey))
        (board Style ConnectiveGoal)
    })
    
    (ai 
        "Hex_ai"
    )
    
)
